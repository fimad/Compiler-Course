structure Ast = Ast
%%

%name Eval

%header (functor EvalLrValsFun(
            structure Token : TOKEN
            structure Ast : AST) 
        )

%pos int
%eop EOF

%left DIM
%left PRINT
%left SEMICOLON
%left NEG
%left MINUS PLUS
%left MULT DIV
%left AND OR
%left LESS LESSEQ MORE MOREEQ EQ NOT

%term ID of string | BOOL of int | FLOAT of real | INT of int | NEG | ASSIGN
    | PLUS | MULT | DIV | MINUS 
    | AND | OR 
    | LESS | LESSEQ | MORE | MOREEQ | EQ | NOT
    | LPAREN | RPAREN | COMMA
    | IN | LET | FUN
    | IF | THEN | ELSE | END
    | FOR | TO | BY | DO | SEMICOLON
    | EMPTY_ARRAY | START_ARRAY | END_ARRAY | POUND
    | PRINT
    | DIM
    | TYPE_BOOL | TYPE_FLOAT | TYPE_INT | COLON
    | EOF
%nonterm LETDEF of Ast.ast
       | EXP of Ast.ast
       | START of Ast.ast
       | PARAMS of (string*LLVM.Type) list
       | EXPLIST of Ast.ast list
       | ARRAY_DIM of int list
       | ARRAY_INDEX of Ast.ast list
       | TERM of Ast.ast
       | STATEMENT of Ast.ast
       | DO_BLOCK of Ast.ast
       | PRIMITIVE_TYPE of LLVM.Type
       | TYPE of LLVM.Type

%%

START : STATEMENT EOF             (STATEMENT)

STATEMENT : EXP                   (EXP)
          | DO_BLOCK              (DO_BLOCK)
          | LETDEF                (LETDEF)

DO_BLOCK : DO EXPLIST END         (Ast.Block(EXPLIST))

EXP : TERM                        (TERM)
    (*digit arith*)
    | PRINT EXP                   (Ast.Print(EXP))
    | DIM LPAREN INT COMMA ID RPAREN                   (Ast.Dim(INT,ID))
    | EXP PLUS EXP                (Ast.Plus(EXP1,EXP2))
    | EXP MINUS EXP               (Ast.Minus(EXP1,EXP2))
    | EXP MULT EXP                (Ast.Mult(EXP1,EXP2))
    | EXP DIV EXP                 (Ast.Div(EXP1,EXP2))
    (*bool arith*)
    | NOT EXP                     (Ast.Not(EXP1))
    | EXP AND EXP                 (Ast.And(EXP1,EXP2))
    | EXP OR EXP                  (Ast.Or(EXP1,EXP2))
    | EXP EQ EXP                  (Ast.Eq(EXP1,EXP2))
    | EXP NOT EQ EXP              (Ast.NotEq(EXP1,EXP2))
    | EXP LESS EXP                (Ast.Less(EXP1,EXP2))
    | EXP LESSEQ EXP              (Ast.LessEq(EXP1,EXP2))
    | EXP MORE EXP                (Ast.More(EXP1,EXP2))
    | EXP MOREEQ EXP              (Ast.MoreEq(EXP1,EXP2))
    | ID ASSIGN EXP               (Ast.Assign(ID,EXP))
    | ID ARRAY_INDEX ASSIGN EXP   (Ast.AssignArray(ID,ARRAY_INDEX,EXP))
    | IF EXP THEN STATEMENT ELSE STATEMENT   (Ast.If(EXP,STATEMENT1,STATEMENT2))
    | FOR LPAREN EXP SEMICOLON EXP SEMICOLON EXP RPAREN STATEMENT   (Ast.For(EXP1,EXP2,EXP3,STATEMENT1))

TERM : ID                         (Ast.Var(ID))
     | ID ARRAY_INDEX             (Ast.ArrayIndex(ID,ARRAY_INDEX))
     | NEG INT                    (Ast.Int(0-INT))
     | NEG FLOAT                  (Ast.Float(0.0-FLOAT))
     | INT                        (Ast.Int(INT))
     | FLOAT                      (Ast.Float(FLOAT))
     | BOOL                       (Ast.Bool(BOOL))
     | START_ARRAY EXPLIST END_ARRAY  (Ast.Array (EXPLIST))
     | START_ARRAY PRIMITIVE_TYPE END_ARRAY ARRAY_DIM      (Ast.EmptyArray (PRIMITIVE_TYPE,ARRAY_DIM))
     | LPAREN EXP RPAREN          (EXP)
     | ID LPAREN EXPLIST RPAREN   (Ast.Apply(Ast.Var(ID),EXPLIST))

ARRAY_INDEX : START_ARRAY EXP END_ARRAY ([EXP])
            | START_ARRAY EXP END_ARRAY ARRAY_INDEX (EXP::ARRAY_INDEX)

ARRAY_DIM : POUND INT             ([INT])
          | POUND INT ARRAY_DIM   (INT::ARRAY_DIM)

EXPLIST : EXP                     ([EXP])
        | EXP COMMA EXPLIST       (EXP::EXPLIST)
        | EXP SEMICOLON EXPLIST       (EXP::EXPLIST)


LETDEF : LET ID EQ STATEMENT IN STATEMENT                           (Ast.Let(ID,STATEMENT1,STATEMENT2))
       | FUN ID LPAREN PARAMS RPAREN COLON TYPE EQ STATEMENT IN STATEMENT   (Ast.Fun(ID,PARAMS,TYPE,STATEMENT1,STATEMENT2))

PARAMS : ID COLON TYPE                      ([(ID,TYPE)])
       | ID COLON TYPE COMMA PARAMS          ((ID,TYPE)::PARAMS)

TYPE : PRIMITIVE_TYPE                                         (PRIMITIVE_TYPE)
     | START_ARRAY PRIMITIVE_TYPE END_ARRAY ARRAY_DIM         (foldr (fn (d,t) => LLVM.array (d,t)) PRIMITIVE_TYPE ARRAY_DIM)

PRIMITIVE_TYPE : TYPE_INT                        (LLVM.i32)
               | TYPE_FLOAT                      (LLVM.float)
               | TYPE_BOOL                       (LLVM.i1)
