structure Ast = Ast
%%

%name Eval

%header (functor EvalLrValsFun(
            structure Token : TOKEN
            structure Ast : AST) 
        )

%pos int
%eop EOF

%left MINUS PLUS
%left MULT DIV
%left LESS LESSEQ MORE MOREEQ EQ NOT

%term ID of string | NUM of int
    | PLUS | MULT | DIV | MINUS 
    | LESS | LESSEQ | MORE | MOREEQ | EQ | NOT
    | LPAREN | RPAREN | COMMA
    | IN | LET | LETSTA | LETDYN
    | IF | THEN | ELSE | END
    | EOF
%nonterm LETDEF of Ast.ast
       | EXP of Ast.ast
       | START of Ast.ast
       | PARAMS of string list
       | EXPLIST of Ast.ast list
       | TERM of Ast.ast
       | STATEMENT of Ast.ast

%%

START : STATEMENT EOF             (STATEMENT)

STATEMENT : EXP                   (EXP)
          | LETDEF                (LETDEF)

EXP : TERM                        (TERM)
    (*digit arith*)
    | EXP PLUS EXP                (Ast.Plus(EXP1,EXP2))
    | EXP MINUS EXP               (Ast.Minus(EXP1,EXP2))
    | EXP MULT EXP                (Ast.Mult(EXP1,EXP2))
    | EXP DIV EXP                 (Ast.Div(EXP1,EXP2))
    (*bool arith*)
    | NOT EXP                     (Ast.Not(EXP1))
    | EXP EQ EXP                  (Ast.Eq(EXP1,EXP2))
    | EXP LESS EXP                (Ast.Less(EXP1,EXP2))
    | EXP LESSEQ EXP              (Ast.LessEq(EXP1,EXP2))
    | EXP MORE EXP                (Ast.More(EXP1,EXP2))
    | EXP MOREEQ EXP              (Ast.MoreEq(EXP1,EXP2))
    | IF EXP THEN STATEMENT ELSE STATEMENT   (Ast.If(EXP,STATEMENT1,STATEMENT2))

TERM : ID                         (Ast.Var(ID))
     | NUM                        (Ast.Num(NUM))
     | LPAREN EXP RPAREN          (EXP)
     | ID LPAREN EXPLIST RPAREN   (Ast.Apply(Ast.Var(ID),EXPLIST))

EXPLIST : EXP                     ([EXP])
        | EXP COMMA EXPLIST       (EXP::EXPLIST)


LETDEF : LET ID EQ STATEMENT IN STATEMENT                           (Ast.Let(ID,STATEMENT1,STATEMENT2))
       | LETSTA ID LPAREN PARAMS RPAREN EQ STATEMENT IN STATEMENT   (Ast.LetSta(ID,PARAMS,STATEMENT1,STATEMENT2))
       | LETDYN ID LPAREN PARAMS RPAREN EQ STATEMENT IN STATEMENT   (Ast.LetDyn(ID,PARAMS,STATEMENT1,STATEMENT2))

PARAMS : ID                       ([ID])
       | ID COMMA PARAMS          (ID::PARAMS)

